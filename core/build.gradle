apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}


// This is the library version used when deploying the artifact
version = "1.0.0"

// 项目的主页
def siteUrl = 'https://github.com/linchengchan/Core'
// Git仓库的url
def gitUrl = 'https://github.com/linchengchan/Core.git'
// Maven Group ID for the artifact，一般填你唯一的包名
group = "com.cc.core"
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'android fast develop'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'interestsoul200jin'     //填写的一些基本信息
                        name 'linchengchan'
                        email 'linchengchan2015@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "CCCore"
        name = "CCCore"   //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
//jav doc采用utf-8编码否则会报“GBK的不可映射字符”错误
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'com.android.support:appcompat-v7:27.1.1'
    compileOnly 'com.android.support.constraint:constraint-layout:1.1.2'
    compileOnly 'com.android.support:recyclerview-v7:27.1.1'
    def lifecycle_version = "1.1.1"
    // ViewModel and LiveData
    api "android.arch.lifecycle:extensions:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"
    api "android.arch.lifecycle:common-java8:$lifecycle_version"
}
